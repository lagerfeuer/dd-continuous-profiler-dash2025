plugins {
    id 'java'
    id 'application'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.sparkjava:spark-core:2.9.4'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.mongodb:mongodb-driver-sync:4.3.2'
    implementation 'com.google.guava:guava:31.0.1-jre'
    implementation 'org.slf4j:slf4j-api:2.0.0'

    runtimeOnly 'ch.qos.logback:logback-classic:1.4.0'
}

processResources {
    from('movies-v2.json.gz')
}

// Shared JVM arguments for all tasks
def sharedJvmArgs = [
    "-Xmx1g",
    "-XX:+ExitOnOutOfMemoryError",
    "-javaagent:dd-java-agent.jar",
    "-Ddd.profiling.enabled=true",
    "-Ddd.profiling.heap.histogram.enabled=true",
    "-XX:FlightRecorderOptions=stackdepth=256",
    "-Ddd.logs.injection=true",
    "-Ddd.trace.sample.rate=1",
    "-Ddd.env=prod",
    "-Ddd.version=${LocalDateTime.now().withNano(0)}",
]

// Task for running the main movies-api-java server
task runIntroServer(type: JavaExec) {
    group = 'application'
    description = 'Runs the intro-movies-api-java server (first part of profiling lab)'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'movies.IntroServer'
    jvmArgs = sharedJvmArgs + [
            "-Ddd.service=intro-movies-api-java",
            "-Ddd.profiling.upload.period=10", // upload every 10s to see data faster
    ]
}

// Task for running the main movies-api-java server
task runServer(type: JavaExec) {
    group = 'application'
    description = 'Runs the movies-api-java server (main api service profiling demos)'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'movies.Server'
    jvmArgs = sharedJvmArgs + [
        "-Ddd.service=movies-api-java",
        "-Ddd.profiling.upload.period=10", // upload every 10s to see data faster
    ]
}

// Task for running the timeline movies-api-java server
task runTimelineServer(type: JavaExec) {
    group = 'application'
    description = 'Runs the timeline movies-api-java server'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'movies.Server'
    jvmArgs = sharedJvmArgs + [
            "-Ddd.service=movies-api-java-timeline",
            "-Ddd.profiling.upload.period=10", // upload every 10s to see data faster
    ]
}

// Task for running the leaky server
task runLeakyServer(type: JavaExec) {
    group = 'application'
    description = 'Runs the LeakyServer (memory-leaking service for profiling demos)'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'movies.LeakyServer'
    jvmArgs = sharedJvmArgs + [
        "-Ddd.service=leaky-api-java",
        "-Ddd.profiling.ddprof.liveheap.enabled=true",
        "-Ddd.profiling.upload.period=60", // we saw heap metric mess up with shorter period
        "-XX:+UseG1GC", // Use G1 garbage collector (better for large heaps)
        "-XX:MaxGCPauseMillis=200", // Target max GC pause time of 200ms
        "-XX:InitiatingHeapOccupancyPercent=45", // Start concurrent GC cycles earlier (down from default 45%)
        "-XX:ConcGCThreads=2", // Dedicate more threads to concurrent GC
        "-XX:G1ReservePercent=20", // Increase reserve to handle promotion failures
    ]
}

java {
    toolchain {
        // not running correctly on instruqt (I think jdk 17 is running)
        languageVersion = JavaLanguageVersion.of(17)
    }
}
